plugins {
    id 'fabric-loom' version '0.8-SNAPSHOT'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven { url = 'https://raw.githubusercontent.com/Devan-Kerman/Devan-Repo/master' }
    maven { url "https://maven.theillusivec4.top" }
    maven { url 'https://dl.bintray.com/ladysnake/libs' }
    maven { url 'https://ladysnake.jfrog.io/artifactory/mods' }
    maven { url 'https://maven.terraformersmc.com/releases' }
    maven { url 'https://maven.blamejared.com' }
    maven { url "https://maven.siphalor.de" }
    maven { url "https://maven.shedaniel.me" }
    maven { url "https://dl.bintray.com/kyrptonaught/customportalapi" }
}

dependencies {

    //Base
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    //APIs
    dependAndInclude("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${cardinal_version}")
	dependAndInclude("io.github.onyxstudios.Cardinal-Components-API:cardinal-components-block:${cardinal_version}")
  	
	dependAndInclude("me.shedaniel.cloth:cloth-config-fabric:${config_api_version}")	
    dependAndInclude("me.sargunvohra.mcmods:autoconfig1u:${auto_config_version}")
	
	modApi("com.terraformersmc:modmenu:${mod_menu_version}")

}

def dependAndInclude(str) {
    dependencies.modApi(str) {
        exclude(group: "net.fabricmc")
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "io.github.prospector")
        exclude(module: "config-2")
    }
    dependencies.include(str) {
        exclude(group: "net.fabricmc")
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "io.github.prospector")
        exclude(module: "config-2")
    }
}


processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

sourceSets {
    main {
        resources {
            srcDirs += project.file("src/generated/resources")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
    // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
    // We'll use that if it's available, but otherwise we'll use the older option.
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}


jar {
    from "LICENSE"
}

// CURSEFORGE STUFF

curseforge {

    def curseProp = new Properties()
    File secretPropsFile = file("key.properties")
    curseProp.load(secretPropsFile.newInputStream())

    project {
        apiKey = curseProp.getProperty('curseKey')
        id = "${curse_id}"
        releaseType = "${curse_release}"
        changelogType = 'text'
        changelog = file('CHANGELOG.txt')
        addGameVersion("${project.minecraft_version}")
        addGameVersion("Fabric")
        addGameVersion("Java 16")

        relations {
            requiredDependency 'fabric-api'
        }
        mainArtifact(remapJar)

        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }
    options {
        forgeGradleIntegration = false
    }
}

